#include <Arduino.h>
#include <BLEDevice.h>
#include <BLEUtils.h>
#include <BLEServer.h>
#include <BLE2902.h>
#include "DHT.h"
#include <Update.h>    // include Update library
#include <WiFi.h>      // include WiFi library
#include <WebServer.h> // include WebServer library
#include <ESPmDNS.h>   // include DNS library

const char *host = "esp32";
// Put your WiFi credentials here....
const char *ssid = "jernet";
const char *password = "";

#define BLE_DEVICE_NAME "ESP32 BLE Sensor Server"
#define BLE_SERVICE_UUID "819dd7a4-c6c5-4c39-b10f-51e180eb1ffb" // Generated by https://www.uuidgenerator.net/ needs to be changed whenever the service is changed (eg characteristics added/removed)
// #define BLE_WATER_TANK_CHARACTERISTIC_UUID "22d8381a-e6df-4ad1-a101-5e2e47c0762b"
#define BLE_SEPTIC_TANK_CHARACTERISTIC_UUID "9910102a-9d4e-41ce-be93-affba54425c4"
#define BLE_TEMPERATURE_CHARACTERISTIC_UUID "c6db06e1-7f34-48ff-9f1e-f2904ac78525"
#define BLE_HUMIDITY_CHARACTERISTIC_UUID "df2be7ec-fb73-40b6-b2cb-3c00d37f2229"
#define DHTTYPE DHT22 // DHT 22
#define DHTPIN 47     // Digital pin connected to the DHT sensor

DHT dht(DHTPIN, DHTTYPE);
bool btConnected = false;
uint8_t LED_PIN = 35;
uint8_t MOSFET_PIN = 48;           // Pin for the MOSFET controlling power to the 24V sensors
uint8_t septicTankLevel1_PIN = 33; // Pin for the septic tank sensor at level 1
uint8_t septicTankLevel2_PIN = 34; // Pin for the septic tank sensor at level 2

BLEServer *pServer = NULL;
BLEService *pService = NULL;
// BLECharacteristic *pCharacteristicWaterTank = NULL;
BLECharacteristic *pCharacteristicSepticTank = NULL;
BLECharacteristic *pCharacteristicTemperature = NULL;
BLECharacteristic *pCharacteristicHumidity = NULL;

// double waterTankLevel = 0.0;
double septicTankLevel = 0.0;
double temperature = 0.0;
double humidity = 0.0;

class BleSensorServerCallbacks : public BLEServerCallbacks
{
  void onConnect(BLEServer *pServer)
  {
    btConnected = true;
    Serial.println("Device connected!");
  };

  void onDisconnect(BLEServer *pServer)
  {
    Serial.println("Device disconnected");
    btConnected = false;
    delay(500);                  // give the bluetooth stack the chance to get things ready
    pServer->startAdvertising(); // restart advertising
    Serial.println("Re-start advertising " + String(BLE_DEVICE_NAME) + "...");
  }
};

BLECharacteristic *createCharacteristic(const char *uuid)
{
  BLECharacteristic *pCharacteristic = pService->createCharacteristic(uuid, BLECharacteristic::PROPERTY_READ | BLECharacteristic::PROPERTY_NOTIFY);
  BLE2902 *pBLE2902 = new BLE2902();
  pBLE2902->setNotifications(true);
  pCharacteristic->addDescriptor(pBLE2902);
  return pCharacteristic;
}

void setupBLE()
{
  Serial.println("Setting up BLE...");
  BLEDevice::init(BLE_DEVICE_NAME);
  // Create the BLE Server
  pServer = BLEDevice::createServer();
  pServer->setCallbacks(new BleSensorServerCallbacks());

  // Create the BLE Service
  pService = pServer->createService(BLE_SERVICE_UUID);

  // Create the BLE Characteristics
  // pCharacteristicWaterTank = createCharacteristic(BLE_WATER_TANK_CHARACTERISTIC_UUID);
  pCharacteristicSepticTank = createCharacteristic(BLE_SEPTIC_TANK_CHARACTERISTIC_UUID);
  pCharacteristicTemperature = createCharacteristic(BLE_TEMPERATURE_CHARACTERISTIC_UUID);
  pCharacteristicHumidity = createCharacteristic(BLE_HUMIDITY_CHARACTERISTIC_UUID);

  // Start the service
  pService->start();

  // Start advertising
  BLEAdvertising *pAdvertising = BLEDevice::getAdvertising();
  pAdvertising->addServiceUUID(BLE_SERVICE_UUID);
  pAdvertising->setScanResponse(false);
  // pAdvertising->setMinPreferred(0x06); // functions that help with iPhone connections issue
  // pAdvertising->setMinPreferred(0x12);
  BLEDevice::startAdvertising();

  Serial.println("BLE Setup Done. Started advertising " + String(BLE_DEVICE_NAME) + "...");
}

void tryConnectWifi()
{
  // Connect to WiFi network
  WiFi.begin(ssid, password);
  // Wait for WiFi connection for max 3 seconds
  int i = 0;
  while (WiFi.status() != WL_CONNECTED && i < 3)
  {
    delay(1000);
    Serial.println("Connecting to WiFi...");
    i++;
  }
  if (WiFi.status() == WL_CONNECTED)
  {
    Serial.println("");
    Serial.print("Connected to ");
    Serial.println(ssid);
    Serial.print("IP address: ");
    Serial.println(WiFi.localIP());
  }
}

void setup()
{
  Serial.begin(115200);
  pinMode(LED_PIN, OUTPUT);
  pinMode(MOSFET_PIN, OUTPUT);
  pinMode(septicTankLevel1_PIN, INPUT);
  pinMode(septicTankLevel2_PIN, INPUT);
  setupBLE();
  tryConnectWifi();
}

void readDhtSensor()
{
  dht.begin();

  // Reading temperature or humidity takes about 250 milliseconds!
  // Sensor readings may also be up to 2 seconds 'old' (its a very slow sensor)
  float h = dht.readHumidity();
  // Read temperature as Celsius (the default)
  float t = dht.readTemperature();

  // Check if any reads failed and exit early (to try again).
  if (isnan(h))
  {
    Serial.println("Failed to read humidity from DHT sensor!");
  }
  else
  {
    humidity = h;
  }

  if (isnan(t))
  {
    Serial.println("Failed to read temperature from DHT sensor!");
  }
  else
  {
    temperature = t;
  }

  Serial.println("Temperature: " + String(temperature) + " C");
}

void readSepticTankSensors()
{
  Serial.println("Reading septic tank sensors...");
  if (digitalRead(septicTankLevel2_PIN) == HIGH)
  {
    septicTankLevel = 90.0;
  }
  else if (digitalRead(septicTankLevel1_PIN) == HIGH)
  {
    septicTankLevel = 50.0;
  }
  else
  {
    septicTankLevel = 0.0;
  }
  Serial.println("Septic tank level: " + String(septicTankLevel) + " %");
}

void readSensors()
{
  Serial.println("Powering on sensors...");
  digitalWrite(MOSFET_PIN, HIGH); // Turn on power to the sensors

  delay(1000); // Wait for the temp sensor to stabilize
  Serial.println("Reading DHT sensor...");
  readDhtSensor();

  delay(3000); // Wait for the septic tank sensor to stabilize
  // Read the septic tank sensor
  readSepticTankSensors();

  digitalWrite(MOSFET_PIN, LOW); // Turn off power to the sensors to preserve energy
  Serial.println("Powering off sensors...");
}

void sendValue(BLECharacteristic *pCharacteristic, double value)
{
  pCharacteristic->setValue(value);
  pCharacteristic->notify();
  Serial.println("Sent value: " + String(value) + " to client for characteristic: " + pCharacteristic->getUUID().toString().c_str());
  delay(500); // bluetooth stack will go into congestion, if too many packets are sent so we need to slow down a bit
}

void sendAllValues()
{
  // sendValue(pCharacteristicWaterTank, waterTankLevel);
  sendValue(pCharacteristicSepticTank, septicTankLevel);
  sendValue(pCharacteristicTemperature, temperature);
  sendValue(pCharacteristicHumidity, humidity);

  // indicate that values was sent
  digitalWrite(LED_PIN, HIGH);
  delay(50);
  digitalWrite(LED_PIN, LOW);
}

// put your main code here, to run repeatedly:
void loop()
{
  readSensors();

  if (btConnected)
  {
    sendAllValues();
  }
  else
  {
    Serial.println("Skipping sending values to client as no client is connected...");
  }

  // sleep for 1 minute
  Serial.println("Going to sleep for 10 seconds...");
  esp_sleep_enable_timer_wakeup(1 * 10 * 1000000);
  esp_deep_sleep_start();
}